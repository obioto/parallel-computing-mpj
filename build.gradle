apply plugin: 'idea'
apply plugin: 'application'

version = '1.0'

mainClassName = 'ch.sebastianhaeni.pancake.ParallelSolver'

def mpjHome = System.getenv('MPJ_HOME')
def mpjStarter = files("$mpjHome/lib/starter.jar")
def mpjJar = files("$mpjHome/lib/mpj.jar")
def mpjClasspath = sourceSets.main.runtimeClasspath - mpjJar

repositories {
    mavenCentral()
}

dependencies {
    compile mpjJar
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

run {
    main = 'runtime.starter.MPJRun'
    classpath = mpjStarter
    args mainClassName, '-cp', mpjClasspath.asPath, '-np', 4
    dependsOn classes
}
